#version 460
#extension GL_EXT_ray_tracing : require
#extension GL_GOOGLE_include_directive : enable
#extension GL_ARB_shader_clock : enable

#include "raycommon.glsl"
#include "sampling.glsl"
#include "wavefront.glsl"

layout(binding = 0, set = 0) uniform accelerationStructureEXT topLevelAS;
layout(binding = 1, set = 0, rgba32f) uniform image2D image;

layout(location = 0) rayPayloadEXT hitPayload prd;

layout(binding = 0, set = 1) uniform CameraProperties{

	mat4 view;
	mat4 proj;
	mat4 viewInverse;
	mat4 projInverse;

}
cam;

layout(push_constant) uniform Constants
{
  vec4  clearColor;
  vec3  lightPosition;
  float lightIntensity;
  float areaLightRadius;
  int   lightType;
  int   maxDepth;
  int   frame;
  float accWeight;
  float mirrorGlossiness;
  float sphereGlossiness;
  float apertureRadius;
  float	focalLenght;
  int	lightIndex;
}
pushC;

void main() 
{
	#define M_PI 3.141592
	
	
	uint seed = tea(gl_LaunchIDEXT.y * gl_LaunchSizeEXT.x + gl_LaunchIDEXT.x, int(clockARB()));

	vec2 d[4] = { vec2(0.25),vec2(0.25, 0.75),vec2(0.75, 0.25),vec2(0.75) };
	vec4 direction[4];


	for(int i = 0; i < 4; i++){

		const vec2 pixelCenter = vec2(gl_LaunchIDEXT.xy) + d[i];
		const vec2 inUV = pixelCenter/vec2(gl_LaunchSizeEXT.xy);
		d[i] = inUV * 2.0 - 1.0;
		
		vec4 target = cam.projInverse * vec4(d[i].x, d[i].y, 1, 1);
		direction[i] = cam.viewInverse * vec4(normalize(target.xyz), 0);
	}

	vec4 origin = cam.viewInverse * vec4(0, 0, 0, 1);

	
	vec3 camRight = cam.view[0].xyz;
	vec3 camUp = cam.view[1].xyz;


	vec3 camForward =  cam.view[2].xyz;
	vec3 camPos = origin.xyz;
	
	vec4 focalPlane;
	vec3 focalPlanePoint = camPos + camForward * pushC.focalLenght;
	focalPlane.xyz = -camForward;
	focalPlane.w = -dot(focalPlane.xyz, focalPlanePoint);
	
	vec3 hitValue = vec3(0);

	int nSamples = 4;

	for(int i = 0; i < nSamples; i++){

		float angle = rnd(seed) * 2.0 * M_PI;
		float radius = sqrt(rnd(seed));
		vec2 offset = vec2(cos(angle), sin(angle)) * radius * pushC.apertureRadius;
		

		vec3 apertPos = camPos + camRight * offset.x + camUp * offset.y;


		vec3 rstart = camPos;
		vec3 rdir = direction[i].xyz;
		float t = abs(-(dot(rstart, focalPlane.xyz) + focalPlane.w) / dot(rdir, focalPlane.xyz));
		
		vec3 focusPos = rstart + rdir * t;
		origin.xyz = apertPos;
		direction[i].xyz = normalize(focusPos - apertPos);

		uint rayFlags = gl_RayFlagsOpaqueEXT;
		float tMin = 0.00001;
		float tMax = 100000.0;

		prd.hitValue = vec3(0);
		prd.depth = 0;
		prd.weight = vec3(1);
		prd.done = 1;
		prd.rayOrigin = origin.xyz;
		prd.rayDir = direction[i].xyz;
		prd.seed = seed;

		vec3 hitValueSample = vec3(0);
		vec3 curWeight = vec3(1);

		for(; prd.depth < pushC.maxDepth; prd.depth++)
		{
		  traceRayEXT(topLevelAS,        // acceleration structure
		              rayFlags,          // rayFlags
		              0xFF,              // cullMask
		              0,                 // sbtRecordOffset
		              0,                 // sbtRecordStride
		              0,                 // missIndex
		              prd.rayOrigin,     // ray origin
		              tMin,              // ray min range
		              prd.rayDir,		 // ray direction
		              tMax,              // ray max range
		              0                  // payload (location = 0)
		  );

		   

		  hitValueSample += prd.hitValue * curWeight;
		  curWeight *= prd.weight;
		}

		hitValue += hitValueSample;

	}

	hitValue /= nSamples;



	float exposure = pushC.accWeight;


	if(pushC.frame > 0){
		
		float a         = 1.0f / float(pushC.frame + 1);
		vec3 oldColor = imageLoad(image, ivec2(gl_LaunchIDEXT.xy)).xyz;
		imageStore(image, ivec2(gl_LaunchIDEXT.xy), vec4(mix(oldColor, hitValue, a), exposure));
		
	
	}else{

		 imageStore(image, ivec2(gl_LaunchIDEXT.xy), vec4(hitValue, exposure));
	}
   
}
